ENTRY(main)
OUTPUT_FORMAT("elf64-x86-64", "elf64-x86-64", "elf64-x86-64")

PAGE_SIZE = 0x1000;
/* Needs to be bigger for DEBUG mode, can be 2 for non debug */
STACK_SIZE = 5 * PAGE_SIZE;

SECTIONS
{
  .text.start (0x40100000) : {
    _kernel_start = .;
    _text_start = .;
    kernel/kernel.o( .text )
    *(.text .text.*)
    _text_end = .;
  }

  .rodata : ALIGN(0x1000) {
    _rodata_start = .;
    *(.rodata .rodata.*)
    *(.got) *(.igot)
    *(.got.plt)  *(.igot.plt)
    _rodata_end = .;
  }

  .swift2_protocol_conformances : {
    swift2_protocol_conformances_start = . ;
    QUAD(SIZEOF(.swift2_protocol_conformances) - 8) ;
    *(.swift2_protocol_conformances) ;
  }

  .data : ALIGN(0x1000) {
    _data_start = .;
    *(.data .data.*)
    _data_end = .;
  }

 /* Thread Local Storage sections  */
  .tdata          : { *(.tdata .tdata.* .gnu.linkonce.td.*) }
  .tbss           : { *(.tbss .tbss.* .gnu.linkonce.tb.*) *(.tcommon) }

  .bss ALIGN(0x1000) : {
    _bss_start = .;
    *(.bss..allocated_pages)
    *(.bss .bss.*)
    . = ALIGN(PAGE_SIZE);
    _guard_page = .;
    . += PAGE_SIZE;
    _stack_start = .;
    . += STACK_SIZE;
    _kernel_stack = .;          /* Top of stack so label comes after */
    _heap_start = .;
    . = ALIGN(0x800000);
    _heap_end = .;

    _bss_end = .;
  }

  . = ALIGN(PAGE_SIZE);
  _kernel_end = .;
}
